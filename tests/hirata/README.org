#+title: Hirata equations

* Parser
:PROPERTIES:
:header-args:lisp: :tangle parser.lisp :comments both
:END:

#+begin_src lisp
(ql:quickload :esrap)
(defpackage :hirata
  (:use :cl :esrap))
(in-package :hirata)

(defrule space (or #\space #\tab))

(defrule number
    (+ (character-ranges (#\0 #\9)))
  (:text t))
(parse 'number #1="654981981")

(defmacro %make-index-rule (name prefix)
  `(defrule ,name
       (and ,prefix number)
     (:function (lambda (m)
                  (intern (format nil "~{~@(~a~)~}" m))))))

(%make-index-rule hole-index "h")
(%make-index-rule particle-index "p")
(defrule index (or hole-index particle-index))

(parse 'hole-index "h65")
(parse 'particle-index "p65")

(defrule op* " * ")

(defrule index-list
    (and space (+ (and index space)))
  (:function (lambda (m) (mapcar #'car (cadr m)))))

(parse 'index-list " h3 h4 p1 p2 ")

(defrule tensor
    (and (+ (not #\space))
         " (" (or index-list " ") ")")
  (:destructure (name _ indices __)
    (declare (ignore _ __))
    `(,(coerce name 'string) ,@(typecase indices
                                 (atom nil)
                                 (t indices)))))

(parse 'tensor "e ( )")
(parse 'tensor "t ( p1 p2 h3 h4 )")

(defrule float (and (or "+ " "- " "") number "." number)
  (:text t))
(progn
  (parse 'float "2.58")
  (parse 'float "+ 2.58")
  (parse 'float "- 2.58"))

(defmacro defrule-sep-by (separator name rule &rest options)
  `(defrule ,name
       (and ,rule (* (and ,separator ,rule)))
     ,@options))

(defrule-sep-by op* tensor-product
  tensor
  (:destructure (fst rest) `(,fst
                             ,@(mapcar #'cadr rest))))

(progn

  (parse 'tensor-product
         (concatenate 'string
                      "Sum ( h3 h4 p1 p2 )"
                      " * t ( p1 p2 h3 h4 )"
                      " * v ( h3 h4 p1 p2 )"))

  (parse 'tensor-product
         (concatenate 'string
                      "Sum ( h8 h9 p7 p10 )"
                      " * t ( p7 h1 ) * t ( p5 h8 ) * t ( p6 h9 )"
                      " * t ( p10 p4 h2 h3 ) * v ( h8 h9 p7 p10 )")))

(defrule permutation
    (and "P("
         index-list
         "=>"
         index-list
         ")")
  (:destructure (_ from __ to ___)
    (declare (ignore _ __ ___))
    `(permute ,from ,to)))

(parse 'permutation "P( p5 p6 p4 h1 h2 h3 => p4 p6 p5 h1 h2 h3 )")

(defrule permutation-list
    (and "[ "
         (+ (and float (? (and op* permutation)) " "))
         "]")
  (:destructure (_ terms __)
    (declare (ignore _ __))
    (mapcar (lambda (term)
              (if (cadr term)
                  (list (car term) (cadadr term))
                  (car term)))
            terms)))

(parse 'permutation-list
       "[ + 1.0 - 1.0 * P( p5 p6 p4 h1 h2 h3 => p4 p6 p5 h1 h2 h3 ) ]")

(parse 'permutation-list
       (concatenate 'string
                    "[ + 1.0"
                    " - 1.0 * P( p5 p6 p4 h1 h2 h3 => p4 p6 p5 h1 h2 h3 )"
                    " + 1.0 * P( p5 p6 p4 h1 h2 h3 => p4 p5 p6 h1 h2 h3 )"
                    " - 1.0 * P( p5 p6 p4 h1 h2 h3 => p5 p6 p4 h2 h1 h3 ) ]"))

(defrule statement
       (and permutation-list
            op*
            tensor-product)
  (:destructure (perms _ tensors)
    (declare (ignore _))
    (list :permutations perms
          :tensors tensors)))

(parse 'statement
       (concatenate 'string
                    "[ + 1.0"
                    " - 1.0 * P( p5 p6 p7 p8 h3 h4 h1 h2 => p5 p7 p6 p8 h3 h4 h1 h2 )"
                    " - 1.0 * P( p6 p5 p7 p8 h3 h4 h1 h2 => p6 p7 p5 p8 h3 h4 h1 h2 )"
                    " - 1.0 * P( p5 p6 p8 p7 h3 h4 h1 h2 => p5 p8 p6 p7 h3 h4 h1 h2 )"
                    " - 1.0 * P( p6 p5 p8 p7 h3 h4 h1 h2 => p6 p8 p5 p7 h3 h4 h1 h2 )"
                    " + 1.0 * P( p5 p6 p7 p8 h3 h4 h1 h2 => p7 p8 p5 p6 h3 h4 h1 h2 )"
                    " + 1.0 * P( p5 p6 p7 p8 h4 h3 h1 h2 => p5 p6 p7 p8 h2 h4 h1 h3 )"
                    " - 1.0 * P( p5 p6 p7 p8 h4 h3 h1 h2 => p5 p7 p6 p8 h2 h4 h1 h3 )"
                    " - 1.0 * P( p6 p5 p7 p8 h4 h3 h1 h2 => p6 p7 p5 p8 h2 h4 h1 h3 )"
                    " - 1.0 * P( p5 p6 p8 p7 h4 h3 h1 h2 => p5 p8 p6 p7 h2 h4 h1 h3 )"
                    " - 1.0 * P( p6 p5 p8 p7 h4 h3 h1 h2 => p6 p8 p5 p7 h2 h4 h1 h3 )"
                    " + 1.0 * P( p5 p6 p7 p8 h4 h3 h1 h2 => p7 p8 p5 p6 h2 h4 h1 h3 )"
                    " + 1.0 * P( p5 p6 p7 p8 h3 h4 h1 h2 => p5 p6 p7 p8 h2 h3 h1 h4 )"
                    " - 1.0 * P( p5 p6 p7 p8 h3 h4 h1 h2 => p5 p7 p6 p8 h2 h3 h1 h4 )"
                    " - 1.0 * P( p6 p5 p7 p8 h3 h4 h1 h2 => p6 p7 p5 p8 h2 h3 h1 h4 )"
                    " - 1.0 * P( p5 p6 p8 p7 h3 h4 h1 h2 => p5 p8 p6 p7 h2 h3 h1 h4 )"
                    " - 1.0 * P( p6 p5 p8 p7 h3 h4 h1 h2 => p6 p8 p5 p7 h2 h3 h1 h4 )"
                    " + 1.0 * P( p5 p6 p7 p8 h3 h4 h1 h2 => p7 p8 p5 p6 h2 h3 h1 h4 )"
                    " + 1.0 * P( p5 p6 p7 p8 h4 h3 h2 h1 => p5 p6 p7 p8 h1 h4 h2 h3 )"
                    " - 1.0 * P( p5 p6 p7 p8 h4 h3 h2 h1 => p5 p7 p6 p8 h1 h4 h2 h3 )"
                    " - 1.0 * P( p6 p5 p7 p8 h4 h3 h2 h1 => p6 p7 p5 p8 h1 h4 h2 h3 )"
                    " - 1.0 * P( p5 p6 p8 p7 h4 h3 h2 h1 => p5 p8 p6 p7 h1 h4 h2 h3 )"
                    " - 1.0 * P( p6 p5 p8 p7 h4 h3 h2 h1 => p6 p8 p5 p7 h1 h4 h2 h3 )"
                    " + 1.0 * P( p5 p6 p7 p8 h4 h3 h2 h1 => p7 p8 p5 p6 h1 h4 h2 h3 )"
                    " + 1.0 * P( p5 p6 p7 p8 h3 h4 h2 h1 => p5 p6 p7 p8 h1 h3 h2 h4 )"
                    " - 1.0 * P( p5 p6 p7 p8 h3 h4 h2 h1 => p5 p7 p6 p8 h1 h3 h2 h4 )"
                    " - 1.0 * P( p6 p5 p7 p8 h3 h4 h2 h1 => p6 p7 p5 p8 h1 h3 h2 h4 )"
                    " - 1.0 * P( p5 p6 p8 p7 h3 h4 h2 h1 => p5 p8 p6 p7 h1 h3 h2 h4 )"
                    " - 1.0 * P( p6 p5 p8 p7 h3 h4 h2 h1 => p6 p8 p5 p7 h1 h3 h2 h4 )"
                    " + 1.0 * P( p5 p6 p7 p8 h3 h4 h2 h1 => p7 p8 p5 p6 h1 h3 h2 h4 )"
                    " + 1.0 * P( p5 p6 p7 p8 h3 h4 h1 h2 => p5 p6 p7 p8 h1 h2 h3 h4 )"
                    " - 1.0 * P( p5 p6 p7 p8 h3 h4 h1 h2 => p5 p7 p6 p8 h1 h2 h3 h4 )"
                    " - 1.0 * P( p6 p5 p7 p8 h3 h4 h1 h2 => p6 p7 p5 p8 h1 h2 h3 h4 )"
                    " - 1.0 * P( p5 p6 p8 p7 h3 h4 h1 h2 => p5 p8 p6 p7 h1 h2 h3 h4 )"
                    " - 1.0 * P( p6 p5 p8 p7 h3 h4 h1 h2 => p6 p8 p5 p7 h1 h2 h3 h4 )"
                    " + 1.0 * P( p5 p6 p7 p8 h3 h4 h1 h2 => p7 p8 p5 p6 h1 h2 h3 h4 ) ]"
                    " * t ( p5 p6 h3 h4 ) * v ( p7 p8 h1 h2 )"))

(defun parse-outfile (path)
  (with-open-file (s path)
    (loop :for line = (read-line s nil 'eol)
          :until (eq line 'eol)
          :collect
            (parse 'statement line))))

(defun save-parse-outfile (in out)
  (with-open-file (s out :direction :output :if-exists :supersede)
    (format s "~S" (parse-outfile in))))
#+end_src

** Parsing test suite
:PROPERTIES:
:header-args:lisp: :tangle t.lisp :comments both
:END:

#+begin_src lisp
(ql:quickload :fiveam)

(in-package :hirata)

(5am:def-suite hirata-parsing)
(defmacro test-parsing (name &rest files)
  `(5am:test (,name :suite hirata-parsing)
     ,(format nil "Test parsing the ~s equations" name)
     ,@(mapcar (lambda (f)
                 `(5am:is (listp (parse-outfile ,f))
                          "Failed parsing file ~s" ,f))
               files)))

(test-parsing ccsdtq
              #P"ccsdtq/ccsdtq_e.out"
              #P"ccsdtq/ccsdtq_t1.out"
              #P"ccsdtq/ccsdtq_t2.out"
              #P"ccsdtq/ccsdtq_t3.out"
              #P"ccsdtq/ccsdtq_t4.out")

(test-parsing ccsdt
              #P"ccsdt/ccsdt_e.out"
              #P"ccsdt/ccsdt_t1.out"
              #P"ccsdt/ccsdt_t2.out"
              #P"ccsdt/ccsdt_t3.out")

(test-parsing ccsd
              #P"ccsd/ccsd_e.out"
              #P"ccsd/ccsd_t1.out"
              #P"ccsd/ccsd_t2.out")

(test-parsing eomccsd
              #P"eomccsd/eomccsd_denominator.out"
              ;; #P"eomccsd/eomccsd_density1.out"
              #P"eomccsd/eomccsd_x1.out"
              #P"eomccsd/eomccsd_x2.out"
              #P"eomccsd/eomccsd_y1.out"
              #P"eomccsd/eomccsd_y2.out")

(test-parsing eomccsdt
              #P"eomccsdt/eomccsdt_denominator.out"
              #P"eomccsdt/eomccsdt_density1.out"
              #P"eomccsdt/eomccsdt_x1.out"
              #P"eomccsdt/eomccsdt_x2.out"
              #P"eomccsdt/eomccsdt_x3.out"
              #P"eomccsdt/eomccsdt_y1.out"
              #P"eomccsdt/eomccsdt_y2.out"
              #P"eomccsdt/eomccsdt_y3.out")

(test-parsing cisd
              #P"cisd/cisd_c1.out"
              #P"cisd/cisd_c2.out"
              #P"cisd/cisd_e.out")

(test-parsing cisdt
              #P"cisdt/cisdt_c1.out"
              #P"cisdt/cisdt_c2.out"
              #P"cisdt/cisdt_c3.out"
              #P"cisdt/cisdt_e.out")

(test-parsing cisdtq
              #P"cisdtq/cisdtq_c1.out"
              #P"cisdtq/cisdtq_c2.out"
              #P"cisdtq/cisdtq_c3.out"
              #P"cisdtq/cisdtq_c4.out"
              #P"cisdtq/cisdtq_e.out")

(fiveam:run! 'hirata-parsing)
#+end_src

#+RESULTS:
: T

** Makefile
:PROPERTIES:
:header-args:makefile: :tangle Makefile
:END:

#+begin_src makefile
OUTFILES = $(shell find . -name \*.out)
LISP_HIRATA_FILES = $(patsubst %.out,%.lisp,$(OUTFILES))

parse: $(LISP_HIRATA_FILES)

%.lisp: %.out
	sbcl \
	--load parser.lisp \
	--eval "(format t \"~S\" (hirata::save-parse-outfile \"$<\" \"$@\"))" \
	--quit

.PHONY: parse
#+end_src




* References
- [[https://hirata-lab.chemistry.illinois.edu/eresources.html][Hirata Group (UIUC)]]
- S. Hirata, The Journal of Physical Chemistry A, 107, 9887–9897 (2003).
- S. Hirata, The Journal of Chemical Physics 121, 51–59 (2004).
- S. Hirata, P.-D. Fan, A. A. Auer, M. Nooijen, and P. Piecuch, The Journal of Chemical Physics 121, 12197–12207 (2004).
- S. Hirata, The Journal of Chemical Physics 122, 094105 (2005).
- P.-D. Fan and S. Hirata, The Journal of Chemical Physics 124, 104108 (2006).
- S. Hirata, Theoretical Chemistry Accounts 116, 2–17 (2006).
- M. Kamiya and S. Hirata, The Journal of Chemical Physics 125, 074111 (2006).
